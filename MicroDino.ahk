;â–¼â–¼â–¼ MICRO DINO â–¼â–¼â–¼
A_IconTip:= "MICRO DINO v0.3 by DavidBevi" ; reddit.com/r/AutoHotkey/comments/185zu9v
A_IconTip.= "`nâ˜» Hold CTRL to run `nâ–² Press SHIFT to jump"

;â–¼ CONTROLS
~Control::Sym   ;(hold)
~^Shift::Jump   ;(press)

;â–¼ SCREEN POSITION
PosX:= "x1325", PosY:= "y1046"

;â–¼ VARIABLES
h:= 0, v:= 0, i:= 0, score:= 0, fs:= 0
xOFagent:= 183

;â–¼ GUI
tray := WinExist("ahk_class Shell_TrayWnd")
MyGui := Gui("-Caption +ToolWindow +AlwaysOnTop -SysMenu +Owner" tray)
MyGui.BackColor := "000000"
WinSetTransColor("000000", MyGui)
MyGui.Show(PosX PosY " w200 h30 NoActivate")
MyGui.SetFont("ccccccc s9")
MyGui.Add("Text", "vObst x200 y18", "â–²")
MyGui.Add("Text", "vScore x" xOFagent-1 " y2 h12 Center", "  0  ")
MyGui.SetFont("ccccccc s10")
MyGui.Add("Text", "vAgent x" xOFagent " y17 h12 Center", " â˜» ")

;â–¼ GUI OVER TASKBAR  tinyurl.com/plankoe-did-this
DllCall("dwmapi\DwmSetWindowAttribute", "ptr",MyGui.hwnd,
        "uint",12, "uint*",1, "uint",4)
hHook:=DllCall("SetWinEventHook", "UInt",0x8005, "UInt",0x800B, "Ptr",0,
       "Ptr",CallbackCreate(WinEventHookProc), "UInt",0, "UInt",0, "UInt",0x2)
WinEventHookProc(p1, p2, p3, p4, p5, p6, p7)
  { if !p3 and p4=0xFFFFFFF7
        return
    SetTimer () => DllCall("SetWindowPos","ptr",  Tray,"ptr",  MyGui.hwnd,"int", 
                   0,"int",  0,"int",  0,"int",  0,"uint",  0x10|0x2|0x200), -1
  }

;â–¼ FUNCTIONS
Sym()
  { hotkey:= SubStr(A_ThisHotkey,2)
    While GetKeyState(hotkey)
    { MyGui["Obst"].Move(i)                       ;â–¼ MOVE OBST
      global i+= 2+score/20
      MyGui["Agent"].Move(,17-h)                  ;â–¼ MOVE AGENT
      global v-= h<0? 0: 0.23
      global v:= h<0? 0: v
      global h:= h<0? 0: h+v
      if abs(xOFagent-i+6)+h < 10                 ;â–¼ IF COLLISION
       { global fs:= score
         global score:= 0
         global i:= -80
       }
      if i > 195                                  ;â–¼ GET POINT
       { global score+= 1
         global i:= 0
       }
      if i < 10                                   ;â–¼ UPDATE SCORE
       { MyGui["Score"].Value:= i<0? fs: score
         MyGui["Agent"].Value:= i<0? "ðŸ˜µ":"â˜»"
         MyGui["Obst"].Redraw()
       }
      Sleep 20                                    ;>> FRAMERATE
    }
  }
Jump()
  { global v+= h>2? 0: 3
    global h:= h>2? h: h+v
  }
